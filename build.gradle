subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'

    sourceCompatibility = 1.8
    version = '1.0-SNAPSHOT'
    group = 'org.rocket'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile 'com.google.guava:guava:16.0.1'
        compile 'com.google.inject:guice:3.0'
        compile 'com.typesafe:config:1.2.0'
        testCompile 'junit:junit:4.11'
        testCompile 'org.mockito:mockito-core:1.9.5'
    }

    task sourcesJar(dependsOn: sources, type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    task javadocJar(dependsOn: javadoc, type: Jar) {
        from javadoc.destinationDir
        classifier = 'javadoc'
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                pom.artifactId = "rocket-${project.name}"
                pom.project {
                    licenses {
                        license {
                            name 'The MIT License'
                            url 'http://www.opensource.org/licenses/mit-license.php'
                            distribution 'repo'
                        }
                    }
                    scm {
                        connection 'git@github.com:Blackrush/Rocket.git'
                    }
                    developers {
                        developer {
                            id 'Blackrush'
                            name 'Antoine CHAUVIN'
                            email 'blackrushx@gmail.com'
                            url 'https://github.com/Blackrush'
                        }
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.10'
}

